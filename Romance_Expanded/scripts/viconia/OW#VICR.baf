// ACTIVATE ROMANCE
IF
	!Global("ViconiaMatch","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("CheckViconiaMatch","GLOBAL",1)
		SetGlobal("ViconiaMatch","GLOBAL",1)
END

// VICONIA LOSES ROMANCE
IF
	InParty(Myself)
    OR(3)
		Global("HexxatRomanceActive","GLOBAL",2)
		Global("RasaadRomanceActive","GLOBAL",2)
		Global("AnomenRomanceActive","GLOBAL",2)
	OR(2)
		Global("ViconiaRomanceActive","GLOBAL",1)
		Global("ViconiaRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("ViconiaRomanceActive","GLOBAL",3)
END

// VICONIA WINS ROMANCE
IF
	InParty(Myself)
	Global("ViconiaRomanceActive","GLOBAL",2)
	OR(2)
		Global("HexxatRomanceActive","GLOBAL",1)
		Global("HexxatRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("HexxatRomanceActive","GLOBAL",3)
END

IF
	InParty(Myself)
	Global("ViconiaRomanceActive","GLOBAL",2)
	OR(2)
		Global("RasaadRomanceActive","GLOBAL",1)
		Global("RasaadRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("RasaadRomanceActive","GLOBAL",3)
END

IF
	InParty(Myself)
	Global("ViconiaRomanceActive","GLOBAL",2)
	OR(2)
		Global("AnomenRomanceActive","GLOBAL",1)
		Global("AnomenRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("AnomenRomanceActive","GLOBAL",3)
END

// VICONIA/HEXXAT CONFLICT
IF
	IfValidForPartyDialog("hexxat")
	IfValidForPartyDialog("viconia")
	OR(2)
		Global("HexxatRomanceActive","GLOBAL",1)
		Global("HexxatRomanceActive","GLOBAL",2)
	Global("OHH_viconia2","Global",1)
	OR(2)
		Global("ViconiaRomanceActive","GLOBAL",1)
		Global("ViconiaRomanceActive","GLOBAL",2)
	Global("OWH_viconiaconflict","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("OWH_viconiaconflict","GLOBAL",1)
		RealSetGlobalTimer("OWH_viconiaconflict_timer","GLOBAL",1200)
		Continue()
END

IF
	IfValidForPartyDialog("hexxat")
	IfValidForPartyDialog("viconia")
	OR(2)
		Global("HexxatRomanceActive","GLOBAL",1)
		Global("HexxatRomanceActive","GLOBAL",2)
	OR(2)
		Global("ViconiaRomanceActive","GLOBAL",1)
		Global("ViconiaRomanceActive","GLOBAL",2)
	Global("OWH_viconiaconflict","GLOBAL",1)
	RealGlobalTimerExpired("OWH_viconiaconflict_timer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("OWH_viconiaconflict","GLOBAL",2)
		Continue()
END

IF
	IfValidForPartyDialog("hexxat")
	IfValidForPartyDialog("viconia")
	CombatCounter(0)
	!AreaType(DUNGEON)
	!See([ENEMY])
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	!StateCheck("Hexxat",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("OWH_viconiaconflict","GLOBAL",2)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

// LT65 - TRUST
IF
	InParty(Myself)
	See(Player1)
	Global("OW#VICTRUST","GLOBAL",1)
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
	PlaySong(40)
	Dialogue(Player1)
END

// LT65 - NO TRUST
IF
	InParty(Myself)
	See(Player1)
	Global("OW#NOTRUST","GLOBAL",1)
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
	PlaySong(40)
	Dialogue(Player1)
END